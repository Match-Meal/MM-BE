plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.12'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.pagoda'
version = '0.0.1-SNAPSHOT'
description = 'matchmeal'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.5'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:3.0.5'
    testImplementation 'org.springframework.security:spring-security-test'
    runtimeOnly 'com.h2database:h2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

// 단위 테스트만 돌리도록 설정 (develop 용)
tasks.named('test') {
    useJUnitPlatform()
    // 'IntegrationTest'로 끝나는 통합테스트 파일은 제외
    exclude '**/*IntegrationTest.class'
}

// 통합테스트 태스크 (Main branch용)
tasks.register('integrationTest', Test) {
    useJUnitPlatform()

    // src/test/java에 있는 코드들을 가져와서 실행
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath

    // IntegrationTest로 끝나는 것만 '포함'
    include '**/*IntegrationTest.class'

    // 단위 테스트 먼저 돌고, 통합테스크가 돌도록 순서 지정
    shouldRunAfter tasks.named('test')
}

// /gradlew build 하면 통합 테스트도 포함되도록 설정
// main 브랜치에서 build 할 때 통합 테스트까지 꼼꼼하게 챙기기 위해서 진행
tasks.named('check') {
    dependsOn tasks.named('integrationTest')
}